GAL22V10
EXPGLUE

/AS   RnW  /DBEN /BGACK /EXPSEL NC NC NC NC NC NC GND
 NC   ADIR  DDIR /DEN    NC     NC NC NC NC NC NC VCC

;
; The expansion glue GAL controls:
; - Data bus buffer enablement
; - Data bus buffer direction
; - Address bus (and other control signals) buffer direction
;
; For normal "CPU is the bus master" operation:
; - ADIR is 0 (-> EXP)
; - DDIR is 1 (-> CPU) for reads, 0 (-> EXP) for writes.  This is
;   essentially the value of RnW, but it is qualified with /AS.
; - /DEN is controlled by /DBEN qualified by /EXPSEL
;
; For "EXP is the bus master" operation:
; - ADIR is 1 (-> system board)
; - DDIR is 0 (-> EXP) for reads, 1 (-> system board) for writes.  This
;   is the inverse of RnW.
; - /DEN is 1
;
; "EXP is the bus master" is true when /BGACK is asserted. The CPU high-Z's
; /AS, /DS, /DBEN, and the address bus, FCx signals, SIZx signals, and RnW
; as soon as the cycle where it asserted /BG has terminated.  Pull-ups should
; be used to ensure these signals aren't left floating.
;
; N.B. ADIR == 1 is a proxy for "EXP is the bus master".
;

ADIR = BGACK;

DDIR = /BGACK * AS * RnW
     +  BGACK * /RnW;

DEN  = BGACK
     + DBEN * EXPSEL;
