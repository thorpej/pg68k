GAL22V10
OBMEM

/UDS /LDS  RnW   /RAMSEL /ROMSEL  A1     A22    A23    A24    A25   NC    GND
 NC   nA1 /RAMUR /RAMLR  /RAMUW  /RAMLW /ROMUR /ROMLR /ROMUW /ROMLW DTACK VCC

;
; On-board memory interface logic for the cpu010.
;
; We decode address bits A[25:22], which gives us 16 banks of
; 4MB each - a whopping 64MB RAM total!
;
; ROM isn't that sophisticated -- it just mirrors within the
; ROM select space.
;
; The on-board RAM uses 4x CY62158H 1M x 8 SRAMs.  These have two chip-enable
; inputs -- /CE1 and CE2, so we can use address line A1 connected to CE2 as
; the discriminator for upper word vs lower word in a longword; we generate
; an inverted copy of A1 for this purpose:
;
;     nA1=1 (A1=0) -> upper word
;     nA1=0 (A1=1) -> lower word
;

nA1 = /A1;
 
; RAM read strobes
RAMUR = RAMSEL * /A22 * /A23 * /A24 * /A25 *  RnW * UDS;
RAMLR = RAMSEL * /A22 * /A23 * /A24 * /A25 *  RnW * LDS;

; RAM write strobes
RAMUW = RAMSEL * /A22 * /A23 * /A24 * /A25 * /RnW * UDS;
RAMLW = RAMSEL * /A22 * /A23 * /A24 * /A25 * /RnW * LDS;

; ROM read strobes
ROMUR = ROMSEL *  RnW * UDS;
ROMLR = ROMSEL *  RnW * LDS;

; ROM write strobes (for in-system programming)
ROMUW = ROMSEL * /RnW * UDS;
ROMLW = ROMSEL * /RnW * LDS;

; DTACK is asserted when any of the strobes are asserted.  This drives
; an open-drain inverter.  These memories are all fast enough that no
; wait states are required for 10MHz system operation.
DTACK = RAMUR
      + RAMLR
      + RAMUW
      + RAMLW
      + ROMUR
      + ROMLR
      + ROMUW
      + ROMLW;

DESCRIPTION

On-board memory control logic for cpu010.
