GAL22V10
TMRGLUE

IOWR   /IORD /CS A2 O0 O1 O2 NC NC NC  NC     GND
/RESET  D0    D1 D2 Q0 Q1 Q2 G0 G1 G2 /TMRSEL VCC

;
; When reset is asserted, clear all the D-FFs (G0-G2)
;
AR = RESET;

;
; When A2=1, access the glue logic.  Otherwise, forward the chip
; select on to the 82c54.
;
TMRSEL = /A2;

;
; Reflect the value of G0-G2 onto the data bus when the read strobe is
; asserted.
;
D0.T = G0;
D0.E = CS * A2 * IORD;

D1.T = G1;
D1.E = CS * A2 * IORD;

D2.T = G2;
D2.E = CS * A2 * IORD;

;
; We have to latch a new value into the D-FF's on every rising edge of
; /IOWR.  Take the value from the data bus if the glue logic is being
; accessed, or the previous value if not.
;
G0.R = D0 *  CS * /TMRSEL
     + G0 *  CS *  TMRSEL
     + G0 * /CS;

G1.R = D1 *  CS * /TMRSEL
     + G1 *  CS *  TMRSEL
     + G1 * /CS;

G2.R = D2 *  CS * /TMRSEL
     + G2 *  CS *  TMRSEL
     + G2 * /CS;

;
; The outputs are enabled when the gates are enabled.
;
Q0 = O0 * G0;
Q1 = O1 * G1;
Q2 = O2 * G2;
