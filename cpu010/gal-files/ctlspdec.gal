GAL22V10
CTLSPDEC

 FC0 FC1  FC2     A1      A2      A3      A4      A5     A6     A7 /UDS   GND
/LDS RnW /CTLSEL /DDUSEL /DDLSEL /CSUSEL /CSLSEL /ERSEL /BERSEL NC /DTACK VCC

;
; Board control space is selected when:
;
;     FC2 * /FC1 * /FC0 * /A3 * /A2 * /A1
;     |_______________|   |_____________|
;           FC==4                0
;
; We make this available on an output pin and also feed that back in for
; our own use.
;
; A7 A6 A5 A4
;  0  0  0  0 - 0 - Diagnostic display register(s)
;  0  0  0  1 - 1 - Configuration switches
;  0  0  1  0 - 2 - System enable register
;  0  0  1  1 - 3 - Bus Error register
;

;
; Diagnostic display register selection (write only).
;
DDUSEL = CTLSEL * /A7 * /A6 * /A5 * /A4 * UDS * /RnW;
DDLSEL = CTLSEL * /A7 * /A6 * /A5 * /A4 * LDS * /RnW;

;
; Configuration switches (read only).
;
CSUSEL = CTLSEL * /A7 * /A6 * /A5 *  A4 * UDS *  RnW;
CSLSEL = CTLSEL * /A7 * /A6 * /A5 *  A4 * LDS *  RnW;

;
; System enable register selection (read/write).
;
ERSEL = CTLSEL * /A7 * /A6 *  A5 * /A4 * UDS;

;
; Bus Error register (read-only).
;
BERSEL = CTLSEL * /A7 * /A6 *  A5 *  A4 * UDS *  RnW;

;
; Generate the /DTACK output.
;
; We assert /DTACK so long as control space is selected and we see
; either data strobe.  All of board control space is fast enough for
; no wait states.  We don't want bus errors happening when control
; space is accessed.
;
DTACK = CTLSEL * UDS
      + CTLSEL * LDS;

DESCRIPTION

Board control space decode logic for the cpu010.
