GAL22V10
TOPDEC

 FC0 FC1    FC2 NC NC NC NC  NC      A26    A27     MMUEN  GND
/AS  DECODE NC  NC NC NC NC /EXPSEL /IOSEL /ROMSEL /RAMSEL VCC

;
; Top-level address decoding for the cpu010.
;
; Physical memory map:
;
; 00xx xxxx.xxxx xxxx.xxxx xxxx.xxxx - RAM
; 01xx xxxx.xxxx xxxx.xxxx xxxx.xxxx - ROM
; 10xx xxxx.xxxx xxxx.xxxx xxxx.xxxx - I/O
; 11xx xxxx.xxxx xxxx.xxxx xxxx.xxxx - expansion
;
; Furthermore, the ROM is always selected if the MMU is not enabled.
;
; These addresses are decoded for {User,Supervisor}{Data,Program}:
;
; FC2 FC1 FC0
;  0   0   0	(Undefined, reserved)
;  0   0   1	User Data Space
;  0   1   0	User Program Space
;  0   1   1	(Undefined, reserved)
;  1   0   0	(Undefined, reserved) - Control Space
;  1   0   1	Supervisor Data Space
;  1   1   0	Supervisor Program Space
;  1   1   1	CPU Space
;
; So, each select equation needs to include:
;
;     /FC2 * /FC1 *  FC0 * AS	; User Data Space
;   + /FC2 *  FC1 * /FC0 * AS	; User Program Space
;   +  FC2 * /FC1 *  FC0 * AS	; Supervisor Data Space
;   +  FC2 *  FC1 * /FC0 * AS	; Supervisor Program Space
;
; This is a lot, so we'll bring it out to a pin to feed back.
;
; N.B. Individual device interface logic is responsible for generating
; /DTACK.
;

DECODE = /FC2 * /FC1 *  FC0 * AS
       + /FC2 *  FC1 * /FC0 * AS
       +  FC2 * /FC1 *  FC0 * AS
       +  FC2 *  FC1 * /FC0 * AS;

RAMSEL = DECODE *  MMUEN * /A27 * /A26;

ROMSEL = DECODE * /MMUEN
       + DECODE *  MMUEN * /A27 *  A26;

IOSEL  = DECODE *  MMUEN *  A27 * /A26;

EXPSEL = DECODE *  MMUEN *  A27 *  A26;

DESCRIPTION

Board control space decode logic for the cpu010.
